@quadriga tetris.base

//tetris.base.Main

@component cat.quadriga.base.Transform;
@component cat.quadriga.base.BoxRenderer;

@component Puntuació {
  int punts  = 0;
  int línies = 0;
  int nivell = 0;
}

@component Taulell {
  int[][] taulell = new int[10][18];
  Entity[][] cubs = new Entity[10][18];
}

@prototype Tetris {
  Transform()
  Puntuació(punts: 0)
  Taulell()
}

@prototype CubDeTaulell(int posX, int posY)
{
  Transform(position: new Vector3f(posX,posY,0))
  BoxRenderer()
}

@event InitTetris {}

@component TetrisGameComponent {
  Entity<Puntuació, Taulell> taulell;
}

@system ControladorDelJoc {
  @components {
    TetrisGameComponent
  }
}

@event InsertarPeça {
  int color;
  int[] px, py;
}

@system LlògicaTaulell {
  @components {
    Puntuació
    Taulell
  }
  
  @event InsertarPeça(
            InsertarPeça event: EVENT, 
            Entity<Puntuació, Taulell> entitat: ENTITY
            )
  {
    int línies = 0;
    int[][] taulell = entitat[Taulell].taulell;
    Entity[][] cubs = entitat[Taulell].cubs;
    for(int peça = 0; peça < 4; ++peça) {
    
      int línea = event.py[peça];
      taulell[event.px[peça]][event.py[peça]] = event.color;
      Entity nouCub = @newEntity(null,entitat);
      @prototype nouCub : CubDeTaulell(
                              posX: event.px[peça], 
                              posY: event.py[peça]);
      cubs[event.px[peça]][event.py[peça]] = nouCub;
      
      boolean netejarLinea = true;
      for(int i = 0; i < 10; ++i) {
        if(entitat[Taulell].taulell[i][event.py[peça]] == 0) {
          netejarLinea = false;
          break;
        }
      }
      if(netejarLinea) {
        for(int i = peça+1; i < 4; ++i) {
          --event.py[i];
        }
        for(int i = 0; i < 10; ++i) {
          @eraseEntity cubs[línea][i];
        }
        for(int i = línea; i < 17; ++i) {
          for(int j = 0; j < 10; ++j) {
            if(i > línea && cubs[i][j] != null) {
              Transform t = cubs[i][j][Transform];
              t.position.y -= 1;
              cubs[i][j][Transform] = t;
            }
            taulell[i][j] = taulell[i+1][j];
          }
        }
        ++línies;
      }
    }
    
    entitat[Taulell].taulell = taulell;;
    entitat[Taulell].cubs = cubs;
    
    entitat[Puntuació].línies += línies;//TODO
    entitat[Puntuació].nivell = entitat[Puntuació].línies / 10;
    entitat[Puntuació].punts += 11;
    if(línies == 1) {
      entitat[Puntuació].punts +=  30;
    } else if(línies == 2) {
      entitat[Puntuació].punts +=  71;
    } else if(línies == 3) {
      entitat[Puntuació].punts += 111;
    } else if(línies == 4) {
      entitat[Puntuació].punts += 160;
    }
  }
}

@thread LlogicaTetris {
  @system ControladorDelJoc;
  @system LlògicaTaulell;
  
  @init {
    Entity tetris = @newEntity("tetris",null);
    @prototype tetris : Tetris();
    Entity e = @newEntity("principal", null); 
    
    @add TetrisGameComponent(taulell: tetris) : e;
   
    int px[] = {0,1,2,3};
    int py[] = {0,0,0,0};
    
    @event InsertarPeça(color: 0, px: px, py: py) : tetris;
  }
}

@thread cat.quadriga.base.SimpleRenderThread

@main Main {
  @thread LlogicaTetris
  @thread SimpleRenderThread
}